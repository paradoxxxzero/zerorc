#!/bin/zsh

############
# 3 Prompt #
############

# Create var shorcuts for colors
___="%{${reset_color}%}"
____="${reset_color}"
for c in red green yellow white black cyan magenta blue; do
    # escaped with %{ %} for use in prompt
    eval _${c}='%{$fg_no_bold[${(L)c}]%}'
    eval __${c}='%{$fg_bold[${(L)c}]%}'
    # not escaped for use anywhere else
    eval ${c}_='$fg_no_bold[${(L)c}]'
    eval ${c}__='$fg_bold[${(L)c}]'
done

local bar="${__white}"

# Are we root ?
if [ "`id -u`" -eq 0 ]; 
then
    bar="${__red}"
else
    case $HOST in
	archwork) 
	    bar="${__magenta}"
	    ;;
	archleeenux)
	    bar="${__cyan}"
	    ;;
	ark)
	    bar="${__green}"
	    ;;
    esac
fi

local top="${bar}┌─"
local bottom="${bar}└"
# if we are in a subshell add a '-' for each sub level to the bottom
if [ $SHLVL -gt 1 ];
then
    for i in {2..$SHLVL}
    do
	bottom="${bottom}─"
    done
fi
bottom="${bottom}> "
local sep="${bar}─"
local time="${__white}[${bar}%*${__white}]"
local user="${__blue}%n"
local at="${__white}@"
local host="${__blue}%m"
local term="${bar}/${__blue}$TERM"
local colon="${_magenta}:"
local dollar="${__white}%#"
local ret="${__white}|${_magenta}%?${__white}|"
local stat="%(?,${__blue}^_^ ,${__yellow}-_-')"
local num="${__white}(${bar}%h${__white})"

# single quotes are important there :
local dir='${bar}${${(%):-%~}//\//${__white}/${__red}}'
local vcinfo='$(X_prompt_vc_info)'

local prmpt="
${top}${time}${sep}─${user}${at}${host}${term}${colon}${dir}${__white}/
${bottom}${dollar}${___} "
local prmpt2="${bottom}${__blue}%_ ${__white}>${___} "

local rprmpt="${bar}<─${__white}${vcinfo}${sep}${ret}${sep}${num}${sep} ${stat}${___}"

export PS1="${prmpt}"
export PS2="${prmpt2}"
export RPS1="${rprmpt}"

# xterm title
case $TERM in
    xterm*)
		precmd () {print -Pn "\e]0;%n@%m: %~\a"}
		;;
esac
